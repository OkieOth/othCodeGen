apply plugin: 'java'
//apply plugin: 'application'
apply plugin: 'groovy'
apply plugin: 'maven'

def vagrantMachineName='ubuntu_psql'


sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

project.group = 'de.othsoft.codeGen'
project.version = '0.1-SNAPSHOT'

/*
 * the following attributes need to be declared in $HOME/.gradle/gradle.properties
    mavenReleaseRepo=http://localhost:8081/content/repositories/releases
    mavenSnapshotRepo=http://localhost:8081/content/repositories/snapshots
    mavenUser=admin
    mavenUserPwd=admin123
    mavenBaseRepo=http://localhost:8081/content/groups/public
 **/

uploadArchives {
    repositories{
        mavenDeployer {
            repository(url: "${mavenReleaseRepo}") {
                    authentication(userName: "${mavenUser}", password: "${mavenUserPwd}")
            }        
            snapshotRepository(url: "${mavenSnapshotRepo}") {
                    authentication(userName: "${mavenUser}", password: "${mavenUserPwd}")
            }        
            pom.version = "${project.version}"
            pom.artifactId = "${project.name}"
            pom.groupId = "${project.group}"        
        }
    }
}

repositories {
    mavenLocal()
    maven {
        url "${mavenBaseRepo}"
    }
    mavenCentral()
}

clean.doFirst {
    delete "src/generated/java/"
    delete "src/generated/resources/"    
    delete "src/test/vagrant/ubuntu_psql/tmp/psql"
    delete "src/test/vagrant/ubuntu_psql/run/ip_address.txt"
    println "src/generated/java/"
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
    compile 'de.othsoft.codeGen:jJdbcDaoRequirements:0.1'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.4'
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile 'postgresql:postgresql:9.1-901-1.jdbc4'
    // testCompile 'org.postgresql:postgresql:9.3-1100-jdbc41'
}


configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration_test/groovy')
        }
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/generated/java')
            srcDir file('src/integration_test/java')
        }
   /* not needed this time
        resources.srcDir file('src/integration-test/resources')
        */
    }
}

task integrationTest(type: Test) {
    doFirst {
        // that's the place to start the test vagrant machine        
        println "project base directory: ${project.rootDir}/src/test/vagrant/ubuntu_psql"
        println 'before integration tests - ' + new Date().getTime()
        "${project.rootDir}/src/test/scripts/startVagrant.sh".execute().in.eachLine {
            println it
        }
        println 'vm started'
        println 'retrieve the ip address of the vm'
       "${project.rootDir}/src/test/scripts/print_vm_address.sh".execute().in.eachLine {
            println it
        }
    }
    
    doLast {
        // that's the place to start the test vagrant machine
        def sout = new StringBuffer()
        def serr = new StringBuffer()
        def proc = "${project.rootDir}/src/test/scripts/stopVagrant.sh".execute()
        proc.consumeProcessOutput(sout,serr)
        proc.waitForOrKill(20000)
        println "out: $sout \n err: $serr"
        println 'after integration tests - ' + new Date().getTime()
    }
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

task copyGeneratedResources {
//    outputs.files file("${project.rootDir}/src/generated/resources/sql")
    println "copy generated resources from '${project.rootDir}/src/generated/resources/sql' to '${project.rootDir}/src/test/vagrant/${vagrantMachineName}/tmp'"
    inputs.dir "${project.rootDir}/src/generated/resources/sql"
    outputs.dir "${project.rootDir}/src/test/vagrant/${vagrantMachineName}/tmp"
    doLast {
        copy {
            from "${project.rootDir}/src/generated/resources/sql"
            into "${project.rootDir}/src/test/vagrant/${vagrantMachineName}/tmp"
        }    
    }
}

integrationTest.dependsOn copyGeneratedResources

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}
