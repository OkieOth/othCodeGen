apply plugin: 'java'
//apply plugin: 'application'
apply plugin: 'groovy'

sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

project.group = 'de.othsoft.codeGen'
project.version = '0.1-SNAPSHOT'


// NetBeans will automatically add "run" and "debug" tasks relying on the
// "mainClass" property. You may however define the property prior executing
// tasks by passing a "-PmainClass=<QUALIFIED_CLASS_NAME>" argument.
//
// Note however, that you may define your own "run" and "debug" task if you
// prefer. In this case NetBeans will not add these tasks but you may rely on
// your own implementation.
/*    print 'Beende Integrationstests'

if (!hasProperty('mainClass')) {
    ext.mainClass = 'de.othsoft.codeGen.dummy.Dummy'
}
*/

// to use enter: gradle upload
uploadArchives {
    repositories {
	flatDir {
		dirs "${System.getenv('HOME')}/myGradleRepos/${project.group}/${project.name}"
	}
    }
}

repositories {
    ivy {
        url "file://${System.getenv('HOME')}/myGradleRepos"
        layout "pattern", {
            artifact "[organisation]/[artifact]/[artifact]-[revision].[ext]"
        }
    }
    mavenCentral()
    // You may define additional repositories, or even remove "mavenCentral()".
    // Read more about repositories here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:repositories
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/generated/java')
            srcDir file('src/integration_test/java')
        }
        /* not needed this time
        resources.srcDir file('src/integration-test/resources')
        */
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.3'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.3'
    testCompile group: 'junit', name: 'junit', version: '4.10'
    integrationTestCompile 'org.slf4j:slf4j-api:1.7.5'
    integrationTestCompile 'ch.qos.logback:logback-classic:1.0.9'
    integrationTestCompile 'de.othsoft.codeGen:jDaoRequirements:0.1-SNAPSHOT'
    integrationTestCompile 'de.othsoft.codeGen:jJdbcDaoRequirements:0.1-SNAPSHOT'
}


task integrationTest(type: Test) {
    doFirst {
        // that's the place to start the test vagrant machine        
        println "project base directory: ${project.rootDir}/src/test/vagrant/ubuntu_psql"
        println 'before integration tests - ' + new Date().getTime()
        def sout = new StringBuffer()
        def serr = new StringBuffer()
        def proc = "${project.rootDir}/src/test/scripts/startVagrant.sh".execute()
        proc.consumeProcessOutput(sout,serr)
        proc.waitForOrKill(20000)
        println "out: $sout \n err: $serr"
    }
    
    doLast {
        // that's the place to start the test vagrant machine
        def sout = new StringBuffer()
        def serr = new StringBuffer()
        def proc = "${project.rootDir}/src/test/scripts/stopVagrant.sh".execute()
        proc.consumeProcessOutput(sout,serr)
        proc.waitForOrKill(20000)
        println "out: $sout \n err: $serr"
        println 'after integration tests - ' + new Date().getTime()
    }
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}